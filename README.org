#+PROPERTY: header-args :tangle init.el :comments org

* user--package

#+begin_src emacs-lisp
(defvar user--package (expand-file-name (file-name-concat user-emacs-directory "elisp"))
  "The directory where user packages are stored.")
(add-to-list 'load-path user--package)
#+end_src

* custom-vars.el
Defines where customs variables are stored and load them.

#+begin_src emacs-lisp
(setq custom-file (locate-user-emacs-file "custom-vars.el"))
(load custom-file 'noerror 'nomessage)
#+end_src

* Buffer behaviour
** File/buffer synch
If the underlying file changes, update the buffer.

#+begin_src emacs-lisp
(global-auto-revert-mode t)
#+end_src

** Unique buffers names
Define sensible buffers names in case of ambiguity.

#+begin_src emacs-lisp
(setq uniquify-buffer-name-style 'post-forward)
(setq uniquify-strip-common-suffix nil)
#+end_src

** History is saved between sessions

#+begin_src emacs-lisp
(require 'savehist)
(savehist-mode)
#+end_src

* Clean calendar
Minimize noise in the calendar.

#+begin_src emacs-lisp
(setq calendar-week-start-day 1)
(setq holiday-bahai-holidays nil)
(setq holiday-hebrew-holidays nil)
(setq holiday-islamic-holidays nil)
(setq holiday-oriental-holidays nil)
#+end_src

* Enhanced commands

#+begin_src emacs-lisp
(defun user--configure-consult ()
  (require 'consult)
  (add-hook 'completion-list-mode-hook #'consult-preview-at-point-mode)
  (advice-add #'register-preview :override #'consult-register-window))
(user--configure-consult)
#+end_src

* Open protected files

#+begin_src emacs-lisp
(require 'epg)
(setq epg-gpg-program (expand-file-name "~/.guix-profile/bin/gpg"))
(require 'epa-file)
(epa-file-enable)
(setq epa-file-name-regexp "\\.\\(gpg\\|asc\\)$")
(epa-file-name-regexp-update)
(setq epa-use-gpg-agent t)
(defun user--protect-secret-files ()
  "Make files with .gpg or .asc extensions read-only when opened or saved."
  (when-let ((file-path (buffer-file-name)))
    (when (string-match-p ".*\\.\\(?:asc\\|gpg\\)$" file-path)
      (read-only-mode 1))))
(add-hook 'find-file-hook #'user--protect-secret-files)
(add-hook 'after-save-hook #'user--protect-secret-files)
#+end_src

* Tabs
Allows Emacs to have tabs.

#+begin_src emacs-lisp
(require 'tab-bar)
(tab-bar-mode)
#+end_src

** C-t ‚Üí new tab

#+begin_src emacs-lisp
(global-set-key (kbd "C-t") #'tab-new)
#+end_src

** M-+ ‚Üí next tab

#+begin_src emacs-lisp
(global-set-key (kbd "M-+") #'tab-bar-switch-to-next-tab)
#+end_src

** M-- ‚Üí previous tab

#+begin_src emacs-lisp
(global-set-key (kbd "M--") #'tab-bar-switch-to-prev-tab)
#+end_src

* Use
** Read
*** Minimize UI noise
Remove whatever is not necessary from the UI.

#+begin_src emacs-lisp
(setq inhibit-startup-screen t)
(setq initial-scratch-message "")
(pixel-scroll-precision-mode -1)
(scroll-bar-mode -1)
(tool-bar-mode -1)
(menu-bar-mode 1)
(setq-default cursor-type 'bar)
(setq-default fill-column 85)
(column-number-mode 1)
(setq frame-title-format '("%b"))
#+end_src

*** Low noise directory UI

#+begin_src emacs-lisp
(require 'dired)
(setq dired-dwim-target t)
(add-hook 'dired-mode-hook #'dired-hide-details-mode)
#+end_src

*** Font
Define a simple and complete (w.r.t. UTF-8) monofont.

#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(font . "JetBrains Mono"))

;; For some reason, the font size is changed unless set last.
(run-with-idle-timer 0.5 nil
                     (lambda ()
                       (set-face-attribute 'default nil
                                           :family "JetBrains Mono"
                                           :height 100)))
#+end_src

*** Highlight matching delimiters

#+begin_src emacs-lisp
(require 'paren)
(show-paren-mode 1)
(require 'rainbow-delimiters)
(add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
#+end_src

*** Dark theme

#+begin_src emacs-lisp
(require 'modus-themes)
(setq modus-themes-hl-line  '(intense accented))
(setq x-underline-at-descent-line t)
(setq modus-themes-intense-markup t)
(setq modus-themes-italic-constructs t
      modus-themes-bold-constructs nil
      modus-themes-region '(bg-only no-extend))
(load-theme 'modus-vivendi-tinted :no-confirm)
#+end_src

*** Highlight diffs on demand

#+begin_src emacs-lisp
(require 'diff-hl)
#+end_src

*** The current line is highlighted

#+begin_src emacs-lisp
(require 'hl-line)
(global-hl-line-mode 1)
#+end_src

*** C-$ ‚Üí clone buffer
Provide multiple ways to look at the same file /e.g./ different narrow-regions.

#+begin_src emacs-lisp
(global-set-key (kbd "C-$") #'clone-indirect-buffer-other-window)
#+end_src

*** dedicate-window

#+begin_src emacs-lisp
(defun dedicate-window (&optional arg)
  "Set current window to be dedicated.
With prefix ARG, undedicate it."
  (interactive "P")
  (set-window-dedicated-p (get-buffer-window (current-buffer)) (not arg))
  (message (if arg
               "Window '%s' is normal"
             "Window '%s' is dedicated")
           (current-buffer)))
#+end_src

*** Show completion preview

#+begin_src emacs-lisp
;; Built-in VS Code-style inline ‚Äúghost text‚Äù completion (Emacs 29+)
(require 'completion-preview)

;; Enable almost everywhere
(dolist (hook '(prog-mode-hook
                text-mode-hook
                comint-mode-hook
                org-mode-hook))
  (add-hook hook #'completion-preview-mode))

;; Settings
(setq completion-preview-minimum-symbol-length 2)   ; don‚Äôt be too eager
(setq completion-preview-exact-match-only nil)      ; show preview even if not exact
(setq completion-preview-ignore-case t)             ; case-insensitive

;; Accept preview with TAB, cycle with M-n/M-p
(define-key completion-preview-active-mode-map (kbd "TAB") #'completion-preview-insert)
(define-key completion-preview-active-mode-map (kbd "M-n") #'completion-preview-next-candidate)
(define-key completion-preview-active-mode-map (kbd "M-p") #'completion-preview-prev-candidate)

;; Make preview appear on normal typing (not just special commands)
(setq completion-preview-commands
      (append completion-preview-commands
              '(self-insert-command
                org-self-insert-command
                paredit-backward-delete)))

;; In programming modes, add cape-dabbrev as a cheap/fast completion source
(add-hook 'completion-preview-mode-hook
          (lambda ()
            (when (derived-mode-p 'prog-mode)
              (add-to-list 'completion-at-point-functions #'cape-dabbrev t))))
#+end_src

** Find
*** Project
Limit the scope of different operations like searching to the relevant root directory.

#+begin_src emacs-lisp
(require 'project)
(setq project-vc-extra-root-markers '("project-root" "mix.exs"))
#+end_src

*** Orderless
Divides the pattern into space-separated components, and matches candidates that
match all of the components in any order is provided.

#+begin_src emacs-lisp
(require 'orderless)
(setq completion-styles '(orderless basic))
(setq completion-category-defaults nil)
(setq completion-category-overrides '((file (styles basic partial-completion))))
#+end_src

*** Annotated completion candidates
Meta-data are added to completion candidates like descriptions.
Cycle annotators using the provided key sequence

#+begin_src emacs-lisp
(require 'marginalia)
(marginalia-mode)
#+end_src

*** Default completion candidates
Cape provides ready-made completion backends.

#+begin_src emacs-lisp
(require 'cape)

;; Global convenient bindings
(global-set-key (kbd "C-c f") #'cape-file)
(global-set-key (kbd "C-c h") #'cape-history)

;; Better completions in shell-mode / eshell / comint
(defun my/setup-shell-completions ()
  (add-to-list 'completion-at-point-functions #'cape-history)
  (add-to-list 'completion-at-point-functions #'pcomplete-completions-at-point))

(add-hook 'shell-mode-hook #'my/setup-shell-completions)

;; Make Cape play nicely with existing completion functions
(advice-add #'comint-completion-at-point     :around #'cape-wrap-nonexclusive)
(advice-add #'pcomplete-completions-at-point :around #'cape-wrap-silent)
(advice-add #'pcomplete-completions-at-point :around #'cape-wrap-purify)
#+end_src

*** C-s ‚Üí Find lines in the current buffer

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "C-s") #'consult-line)
#+end_src

*** M-g g ‚Üí Find line by number

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "M-g g") #'consult-goto-line)
#+end_src

*** M-i ‚Üí Find sections in the current buffer
*** M-. ‚Üí Find the definition of some symbol

#+begin_src emacs-lisp
(require 'xref)
(global-set-key (kbd "M-.") #'xref-find-definitions)
#+end_src

*** C-r ‚Üí Find files that contains some strings

#+begin_src emacs-lisp
(require 'rg)
(rg-enable-default-bindings)
(setq rg-command-line-flags '())
(user--configure-consult)
(global-set-key (kbd "C-r") #'consult-ripgrep)
#+end_src

*** C-c f ‚Üí Find files by name

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "C-c f") #'consult-fd)
#+end_src

*** C-- ‚Üí Find make among all marks

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "C-- ") #'consult-mark)
#+end_src

*** C-x b ‚Üí Find a buffer among all buffers

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "C-x b") #'consult-buffer)
#+end_src

*** C-y ‚Üí Find what to paste at point

#+begin_src emacs-lisp
(user--configure-consult)
(global-set-key (kbd "C-y") #'consult-yank-replace)
#+end_src

*** C-z ‚Üí Find file among recent files

#+begin_src emacs-lisp
(require 'recentf)
(recentf-mode)
(setq recentf-max-menu-items 20)
(setq recentf-max-saved-items 100)
(run-at-time nil (* 5 60) 'recentf-save-list)
(user--configure-consult)
(global-set-key (kbd "C-z") #'consult-recent-file)
#+end_src

*** M-/ ‚Üí Find the next thing to instert

#+begin_src emacs-lisp
(require 'dabbrev)
(global-set-key (kbd "M-/") #'dabbrev-completion)
(add-to-list 'dabbrev-ignored-buffer-regexps "\\` ")
(add-to-list 'dabbrev-ignored-buffer-modes 'doc-view-mode)
(add-to-list 'dabbrev-ignored-buffer-modes 'pdf-view-mode)
(add-to-list 'dabbrev-ignored-buffer-modes 'tags-table-mode)
#+end_src

** Select
*** C-; ‚Üí Select all occurrences of the current word

#+begin_src emacs-lisp
(require 'iedit)
(global-set-key (kbd "C-;") #'iedit-mode)
#+end_src

*** C-< ‚Üí Expand selected region

#+begin_src emacs-lisp
(require 'expand-region)
(global-set-key (kbd "C-<") #'er/expand-region)
#+end_src

*** Vertical menus to choose from

#+begin_src emacs-lisp
(require 'vertico)
(vertico-mode)
(setq vertico-scroll-margin 0)
(setq vertico-count 20)
(setq vertico-resize t)
(setq vertico-cycle t)
;; Optional: Enable vertico-multiform for better completion UI
(vertico-multiform-mode)
(setq vertico-multiform-categories
      '((file grid)
        (consult-grep buffer)))
#+end_src

*** M-m ‚Üí Select multiple lines

#+begin_src emacs-lisp
(require 'multiple-cursors)
(global-set-key (kbd "M-m") #'mc/edit-lines)
#+end_src

*** In-buffer completion
Open a small completion popup at point.

#+begin_src emacs-lisp
(require 'corfu)
(setq corfu-cycle t)
(setq corfu-auto nil)                  ; we drive completion manually
(setq corfu-quit-no-match 'separator)
(setq corfu-auto-prefix 2)
(setq corfu-auto-delay 0.1)
(setq corfu-popupinfo-delay '(0.5 . 0.2))
(setq corfu-preview-current 'insert)   ; shows the candidate as if typed
(setq corfu-preselect 'prompt)         ; first candidate is not auto-selected
(setq corfu-on-exact-match nil)        ; don't auto-insert on exact match

;; Key bindings inside the Corfu minibuffer map
(define-key corfu-map (kbd "M-SPC")   #'corfu-insert-separator)
(define-key corfu-map (kbd "TAB")     #'corfu-next)
(define-key corfu-map [tab]           #'corfu-next)
(define-key corfu-map (kbd "S-TAB")   #'corfu-previous)
(define-key corfu-map [backtab]       #'corfu-previous)
(define-key corfu-map (kbd "RET")     #'corfu-insert)

;; Enable the modes
(corfu-history-mode 1)
(corfu-popupinfo-mode 1)
(global-corfu-mode 1)

;; corfu-candidate-overlay gives ‚ÄúVS Code-style‚Äù inline ghost text
(require 'corfu-candidate-overlay)
(corfu-candidate-overlay-mode +1)

;; Convenient global keys to trigger completion-at-point manually
(global-set-key (kbd "C-<tab>") #'completion-at-point)
(global-set-key (kbd "C-!") #'completion-at-point)  ; C-! is the same as above in your config
#+end_src

** Remember
*** C-c l ‚Üí Remember places in org mode files
*** Remember recently executed commands

#+begin_src emacs-lisp
(require 'repeat)
#+end_src

** Move
*** M-i ‚Üí list and jump to sections of the current buffer

#+begin_src emacs-lisp
(require 'imenu)
(setq imenu-auto-rescan t)
(user--configure-consult)
(global-set-key (kbd "M-i") #'consult-imenu)
#+end_src

*** Jump to location, keyboard only

#+begin_src emacs-lisp
(require 'avy)
(global-set-key (kbd "C-:") #'avy-goto-char)
#+end_src

*** Click on =[[id: ‚Ä¶]]= or =[[ref: ‚Ä¶]]= ‚Üí list file candidates

#+begin_src emacs-lisp
(add-to-list 'load-path (file-name-concat user--package "locs-and-refs/_dist"))
(require 'locs-and-refs)
(modus-themes-with-colors
  (custom-set-faces
   `(locs-and-refs-location-face
     ((t (:foreground ,magenta-faint :underline t))))
   `(locs-and-refs-reference-face
     ((t (:foreground ,red-faint :underline t))))))
(locs-and-refs-mode)
#+end_src

** Edit
*** Do not GC when editing

#+begin_src emacs-lisp
(require 'gcmh)
(setq gcmh-verbose t)
(gcmh-mode)
#+end_src

*** Avoid littering the filesystem
Just stop.

#+begin_src emacs-lisp
(defconst emacs-tmp-dir (expand-file-name (format "emacs%d" (user-uid)) temporary-file-directory))
(setq create-lockfiles nil)
(setq delete-by-moving-to-trash t)
(setq
 backup-by-copying t
 delete-old-versions t
 kept-new-versions 6
 kept-old-versions 2
 version-control t
 auto-save-list-file-prefix emacs-tmp-dir
 auto-save-file-name-transforms `((".*" ,emacs-tmp-dir t))
 backup-directory-alist `((".*" . ,emacs-tmp-dir)))
#+end_src

*** Low tabs
Minimize tabs usage.

#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
#+end_src

*** Low noise
Minimze the numbfer of "training wheels".

#+begin_src emacs-lisp
(put 'downcase-region 'disabled nil)
(put 'upcase-region 'disabled nil)
(delete-selection-mode 1)
(put 'narrow-to-region 'disabled nil)
(fset 'yes-or-no-p 'y-or-n-p)
(setq ring-bell-function 'ignore)
#+end_src

*** unfill-paragraph

#+begin_src emacs-lisp
(defun unfill-paragraph (&optional region)
  "Takes a multi-line paragraph and makes it into a single line of text."

  (interactive (progn (barf-if-buffer-read-only) '(t)))
  (let ((fill-column (point-max))
        ;; This would override `fill-column' if it's an integer.
        (emacs-lisp-docstring-fill-column t))
    (fill-paragraph nil region)))
#+end_src

*** Insert parentheses by pairs

#+begin_src emacs-lisp
(electric-pair-mode 1)
#+end_src

*** Shortcut ‚Üí Snippet inserted

#+begin_src emacs-lisp
(require 'f)
(require 'yasnippet)
(setq yas--default-user-snippets-dir nil)
(add-to-list 'yas-snippet-dirs (f-join user-emacs-directory "snippets"))
(setq yas-new-snippet-default
      "# -*- mode: snippet -*-
# name: $1
# key: ${2:${1:$(yas--key-from-desc yas-text)}}
# expand-env: ((yas-indent-line 'fixed) (yas-wrap-around-region 'nil))
# --
$0`(yas-escape-text yas-selected-text)`")
(yas-global-mode)


(defcustom auto-replace-characters-include-minibuffer nil
  "If non-nil, perform replacements in the minibuffer too.
  Default nil because changing text in the minibuffer can interfere with
  completions and special minibuffer behavior."
  :type 'boolean
  :group 'convenience)

(defvar auto-replace-characters-rules
  '((",:"   . "‚Üí")
    (",iso" . "‚âÖ")
    (",|-" . "‚ä¢")
    (",..." . "‚Ä¶")
    (",a" . "Œ±")
    (",b" . "Œ≤")
    (",x" . "√ó")
    (",;" . ":‚â°")
    (",---" . "‚â°")
    (",fa" . "‚àÄ")
    (",phi" . "œÜ")
    ("/=" . "‚â†")
    ("O/" . "‚àÖ")
    (",>=" . "‚â•")
    (",=<" . "‚â§")
    (",in" . "‚àà")
    (",<-" . "‚Üê")
    (",=>" . "‚áí")
    (",<=" . "‚áê")
    (",la" . "Œª")
    (",!" . "‚Üì")
    (",>" . "‚ñ∏")
    (",h" . "üûé"))
  "Alist of (STRING . REPLACEMENT).
  When STRING appears just before point, replace it with REPLACEMENT.")

(defun auto-replace-characters--post-self-insert ()
  "Check recently typed text and replace according to `auto-replace-characters-rules'."
  (when (and (or auto-replace-characters-include-minibuffer (not (minibufferp)))
             (not buffer-read-only))
    (dolist (rule auto-replace-characters-rules)
      (let* ((len (length (car rule)))
             (start (- (point) len)))
        (when (and (>= (point) len)
                   (string= (buffer-substring-no-properties start (point))
                            (car rule)))
          (atomic-change-group
            (delete-region start (point))
            (insert (cdr rule))))))))

(define-minor-mode auto-replace-characters-mode
  "Minor mode: replace strings like \"->\" with symbols as you type.
  See `auto-replace-characters-rules' for customization."
  :lighter " ARC"
  (if auto-replace-characters-mode
      (add-hook 'post-self-insert-hook #'auto-replace-characters--post-self-insert nil t)
    (remove-hook 'post-self-insert-hook #'auto-replace-characters--post-self-insert t)))

(define-globalized-minor-mode global-auto-replace-characters-mode
  auto-replace-characters-mode
  (lambda ()
    (when (or auto-replace-characters-include-minibuffer (not (minibufferp)))
      (auto-replace-characters-mode 1)))
  "Globalized version of `auto-replace-characters-mode'.")

(add-hook 'org-mode-hook #'auto-replace-characters-mode)
#+end_src

** Execute
*** Recursive minibuffer
Allows recursive minibuffer so that multiple commands may be issued.

#+begin_src emacs-lisp
(setq enable-recursive-minibuffers t)
#+end_src

* Languages
** CSS

#+begin_src emacs-lisp
;; Use tree-sitter-based CSS mode instead of the old one
(add-to-list 'auto-mode-alist '("\\.css\\'" . css-ts-mode))

;; Remap the legacy css-mode to the new tree-sitter version
(add-to-list 'major-mode-remap-alist '(css-mode . css-ts-mode))
#+end_src

** JavaScript
** Python

#+begin_src emacs-lisp
(require 'python)
(setq python-indent-offset 4)
(setq python-indent-guess-indent-offset nil)
(require 'pyvenv)
(require 'reformatter)
(reformatter-define ruff-format
  :program "ruff"
  :args '("format" "--stdin-filename" "temp.py" "-")
  :stdin t)
(defun reformatter-python-code-block ()
  (message "TODO"))
(add-hook 'python-mode-hook #'reformatter-python-code-block)
#+end_src

** HTML

#+begin_src emacs-lisp
(require 'web-mode)
;; Use tree-sitter-based CSS mode instead of the old one
(add-to-list 'auto-mode-alist '("\\.html?\\'" . web-mode))

(setq web-mode-indent-style 1)
(setq web-mode-markup-indent-offset 2)
(setq web-mode-css-indent-offset 2)
(setq web-mode-code-indent-offset 2)
#+end_src

** Bash

#+begin_src emacs-lisp
;; Bash files open in tree-sitter mode
(add-to-list 'auto-mode-alist '("\\.bash\\'" . bash-ts-mode))

;; Remap legacy sh-mode ‚Üí bash-ts-mode everywhere
(add-to-list 'major-mode-remap-alist '(sh-mode . bash-ts-mode))

;; Indentation
(setq sh-basic-offset 2
      sh-indentation 2)

;; Start Eglot automatically in bash-ts-mode
(add-hook 'bash-ts-mode-hook #'eglot-ensure)

;; Optional friendly warning if bash-language-server is missing
(when (not (executable-find "bash-language-server"))
  (message "WARNING: bash-language-server not found in PATH; Bash LSP features will be unavailable."))
#+end_src

** Elixir

#+begin_src emacs-lisp
;; Load local tree-sitter modes
(add-to-list 'load-path (file-name-concat user--package "elixir-ts-mode"))
(add-to-list 'load-path (file-name-concat user--package "heex-ts-mode"))

;; Install grammars the first time (idempotent)
(unless (treesit-language-available-p 'elixir)
  (elixir-ts-install-grammar))
(unless (treesit-language-available-p 'heex)
  (heex-ts-install-grammar))

;; Autoload the new modes
(autoload 'elixir-ts-mode "elixir-ts-mode" "Major mode for Elixir (tree-sitter)" t)
(autoload 'heex-ts-mode   "heex-ts-mode"   "Major mode for Heex"               t)

;; File associations
(add-to-list 'auto-mode-alist '("\\.ex\\'"  . elixir-ts-mode))
(add-to-list 'auto-mode-alist '("\\.exs\\'" . elixir-ts-mode))
(add-to-list 'auto-mode-alist '("\\.heex\\'" . heex-ts-mode))

;; Remap the old non-tree-sitter mode to the new one
(add-to-list 'major-mode-remap-alist '(elixir-mode . elixir-ts-mode))

;; Start Eglot automatically in Elixir files
(add-hook 'elixir-ts-mode-hook #'eglot-ensure)

;; Tell Eglot where the elixir-ls script lives
(require 'eglot)
(add-to-list 'eglot-server-programs
             `(elixir-ts-mode . ,(file-name-concat (expand-file-name "~") "bin/elixir-ls/language_server.sh")))
#+end_src

** Lisp
*** Emacs lisp

#+begin_src emacs-lisp
(require 'paredit)
(add-hook 'emacs-lisp-mode-hook #'paredit-mode)
#+end_src

*** Scheme

#+begin_src emacs-lisp
(require 'paredit)
(add-hook 'scheme-mode-hook #'paredit-mode)
(require 'geiser)
(add-hook 'scheme-mode-hook #'geiser-mode)
(setq geiser-active-implementations '(guile))
(setq geiser-default-implementation 'guile)
(require 'macrostep)
(require 'macrostep-geiser)
(add-hook 'geiser-mode-hook #'macrostep-geiser-setup)
(add-hook 'geiser-repl-mode-hook #'macrostep-geiser-setup)
#+end_src

** Makefile

#+begin_src emacs-lisp
(require 'make-mode)
(add-hook 'makefile-mode-hook #'user--indent-makefile-code-blocks-with-tabs)
(defun user--indent-makefile-code-blocks-with-tabs ()
  (setq indent-tabs-mode t))
#+end_src

** Org
:PROPERTIES:
:ID:       cc511d66-615b-4a92-8481-fb63ba23c43f
:END:

#+begin_src emacs-lisp
(require 'org)

(global-set-key (kbd "C-c C-l") #'org-insert-link)
(global-set-key (kbd "M-RET")    #'org-insert-heading)
(global-set-key (kbd "M-<right>") #'org-metaright)
(global-set-key (kbd "M-p")      #'org-copy-outline-path-to-kill-ring)
(global-set-key (kbd "C-c i")    #'org-clock-in)
(global-set-key (kbd "C-c o")    #'org-clock-out)
(global-set-key (kbd "C-c a")    #'org-agenda)

;; Appearance & behaviour
(setq org-cite-global-bibliography
      (list (expand-file-name "~/src/bibliography/reference.bib")))
(setq org-cite-csl-styles-dir (expand-file-name "~/.emacs.d/csl"))
(setq org-cite-export-processors '((latex biblatex) (t csl)))

(setq org-emphasis-alist (delete '("+" (:strike-through t)) org-emphasis-alist))
(setq org-fontify-emphasized-text t)
(setq org-startup-folded 'show2levels)
(setq org-ellipsis " ")
(setq org-hide-leading-stars t)
(setq org-startup-indented nil)
(setq org-src-fontify-natively t)
(setq org-id-link-to-org-use-id t)
(setq org-link-keep-stored-after-insertion t)
(setq org-imenu-depth 100)
(setq org-src-preserve-indentation t)
(setq org-hide-emphasis-markers t)

(setq org-babel-python-command-nonsession "python3")

(setq org-todo-keywords
      '((sequence "TODO(t)" "WAITING(w)" "DOING(o)" "|" "DONE(d)" "FAILED(f)" "CANCELED(c)")))

(setq org-todo-keyword-faces
      '(("TODO"     . (:foreground "red"     :weight bold))
        ("WAITING"  . (:foreground "orange"  :weight bold))
        ("DOING"    . (:foreground "orange"  :weight bold))
        ("FAILED"   . (:foreground "purple"  :weight bold))
        ("DONE"     . (:foreground "green"   :weight bold))
        ("CANCELED" . (:foreground "gray60" :weight bold))))

(setq org-log-into-drawer t)
(setq org-clock-into-drawer t)

(add-to-list 'org-src-lang-modes '("js"   . js-ts))
(add-to-list 'org-src-lang-modes '("json" . js-ts))
(add-to-list 'org-src-lang-modes '("heex" . heex-ts))

(org-babel-do-load-languages
 'org-babel-load-languages
 '((emacs-lisp . t)
   (C          . t)
   (css        . t)
   (shell      . t)
   (lisp       . t)
   (scheme     . t)
   (dot        . t)
   (awk        . t)
   (R          . t)
   (python     . t)
   (js         . t)))

;; Fix for citeproc (some CSL styles expect this function)
(unless (fboundp 'citeproc-style-cite-superscript-p)
  (defun citeproc-style-cite-superscript-p (x) t))

;; Helper functions
(defun org-copy-outline-path-to-kill-ring ()
  "Copy current heading‚Äôs full outline path (Org syntax) to kill ring."
  (interactive)
  (let ((path (org-format-outline-path (org-get-outline-path t) 10000)))
    (kill-new path)
    (message "Outline path ‚Üí kill-ring: %s" path)))

(defun org-for-all-headings-add-id ()
  "Add missing ID property to every heading in the buffer."
  (interactive)
  (org-map-entries
   (lambda ()
     (unless (org-entry-get (point) "ID")
       (org-entry-put (point) "ID" (org-id-new))))))

(defun org-show-all-drawers ()
  "Reveal all property drawers in the current buffer."
  (interactive)
  (org-element-map (org-element-parse-buffer) 'property-drawer
    (lambda (drawer) (org-fold-hide-drawer-toggle 'off nil drawer))))

;; Remove :PROPERTIES: drawers after tangling (keeps tangled files clean)
(defun user--org-remove-property-drawers ()
  "Delete all :PROPERTIES:/:END: blocks in the current buffer after tangling."
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward "^[ \t]*:PROPERTIES:$" nil t)
      (let ((start (line-beginning-position)))
        (when (re-search-forward "^[ \t]*:END:" nil t)
          (delete-region start (line-end-position 0))
          (delete-blank-lines))))
    (when (buffer-modified-p) (save-buffer))))
(add-hook 'org-babel-post-tangle-hook #'user--org-remove-property-drawers)

;; Org Agenda

(setq org-agenda-skip-scheduled-if-done nil)
(setq org-agenda-span 'day)
(setq org-agenda-show-future-repeats 'next)
(setq org-deadline-warning-days 1)
(setq org-agenda-sorting-strategy '(time-up todo-state-down priority-down))
(setq org-agenda-prefix-format
      '((agenda . "%s %-12t %b")
        (todo   . "%b")
        (tags   . "%b")
        (search . "%b")))
(setq org-agenda-time-grid
      '((daily today require-timed)
        (600 2400)
        " ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ " "‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ‚îÑ"))

(defun user--set-org-agenda-files (&rest _)
  "Dynamically collect all .org files that contain TODO keywords."
  (let* ((keywords (with-temp-buffer
                     (org-mode)
                     (concat "(" (mapconcat #'identity org-todo-keywords-1 "|") ")")))
         (cmd (format "rg -u -m1 -l -g '*.org' '^\\*+ +%s' ~" keywords))
         (files (split-string-and-unquote (shell-command-to-string cmd) "\n")))
    (setq org-agenda-files
          (seq-filter #'user--set-org-agenda-files-filters files))))
(advice-add #'org-agenda :before #'user--set-org-agenda-files)

(defun user--set-org-agenda-files-filters (file)
  "Exclude certain directories from org-agenda-files."
  (not (or (string-match-p "/worg/" file)
           (string-match-p "/dl-python-extracteur/tests/" file)
           (string-match-p "/dl-python-docparser/tests/" file))))

;; Citations
(require 'citeproc)
#+end_src

** systemd

#+begin_src emacs-lisp
(require 'systemd)
#+end_src

** Dot

#+begin_src emacs-lisp
(require 'graphviz-dot-mode)
(setq graphviz-dot-indent-width 2)
#+end_src

** TypeScript

#+begin_src emacs-lisp
;; Tree-sitter grammars ‚Äî install once via M-x treesit-install-language-grammar
;; Add TypeScript and TSX grammar recipes (place this early in your init.el)
(require 'treesit)
(add-to-list 'treesit-language-source-alist (cons 'typescript '("https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")))
(add-to-list 'treesit-language-source-alist (cons 'tsx '("https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")))
(unless (treesit-language-available-p 'typescript)
  (treesit-install-language-grammar 'typescript))
(unless (treesit-language-available-p 'tsx)
  (treesit-install-language-grammar 'tsx))

;; Use tree-sitter modes everywhere
(dolist (mapping '((typescript-mode . typescript-ts-mode)
                   (tsx-mode        . tsx-ts-mode)
                   (js-mode         . typescript-ts-mode)
                   (js-json-mode    . json-ts-mode)))
  (add-to-list 'major-mode-remap-alist mapping))

;; File associations
(add-to-list 'auto-mode-alist '("\\.ts\\'"  . typescript-ts-mode))
(add-to-list 'auto-mode-alist '("\\.tsx\\'" . tsx-ts-mode))
(add-to-list 'auto-mode-alist '("\\.mts\\'" . typescript-ts-mode))
(add-to-list 'auto-mode-alist '("\\.cts\\'" . typescript-ts-mode))

;; Indentation (2 spaces, as is standard in the JS/TS world)
(setq typescript-ts-mode-indent-offset 2)

;; Start Eglot automatically ‚Äî works with typescript-language-server
(add-hook 'typescript-ts-mode-hook #'eglot-ensure)
(add-hook 'tsx-ts-mode-hook        #'eglot-ensure)

;; Tell Eglot to use typescript-language-server for both .ts and .tsx
(require 'eglot)
(add-to-list 'eglot-server-programs
             '((typescript-ts-mode tsx-ts-mode)
               . ("typescript-language-server" "--stdio")))

;; Prettier formatting on save (requires prettier in PATH or in node_modules)
(require 'apheleia)
(setf (alist-get 'prettier apheleia-formatters)
      '("prettier" "--stdin-filepath" filepath))
(dolist (mode '(typescript-ts-mode tsx-ts-mode))
  (setf (alist-get mode apheleia-mode-alist) 'prettier))
(apheleia-global-mode +1)

;; Make local node_modules/.bin available (very useful in monorepos)
(require 'add-node-modules-path)
(add-hook 'typescript-ts-mode-hook #'add-node-modules-path)
(add-hook 'tsx-ts-mode-hook        #'add-node-modules-path)

;; Optional: respect .envrc / direnv for correct Node version
(when (executable-find "direnv")
  (require 'direnv)
  (direnv-mode +1))
#+end_src

* I/O performances
Bigger chunks, faster I/O.

#+begin_src emacs-lisp
(setq read-process-output-max (* 1024 1024))
#+end_src

* Application
List standalone applications that runs on Emacs or are external processes to which
Emacs has an interface for.

** TotalRecall

#+begin_src emacs-lisp
(add-to-list
 'load-path
 (expand-file-name "~/src/total-recall-next/total-recall-0.11/elisp"))

(require 'total-recall)
(setq total-recall-database
   (file-name-concat
    (expand-file-name user-emacs-directory) "total-recall.sqlite3"))
#+end_src

** IRC

#+begin_src emacs-lisp
(require 'circe)
(setq circe-use-cycle-completion t)
(setq circe-case-insensitive-completion t)
(setq circe-format-say "{nick:-16s}: {body}")
(setq circe-format-self-say "<{nick:-16s}>: {body}")
(setq circe-display-activity nil)
(setq circe-network-options
      `(("Libera Chat"
         :nick "phfrohring"
         :channels ("#emacs" "#guix")
         :nickserv-password ,(getenv "NICKSERVPWD"))))
(setq circe-reduce-lurker-spam t)
(add-hook 'circe-channel-mode-hook #'enable-circe-color-nicks)
#+end_src

** Git

#+begin_src emacs-lisp
(require 'magit)
(defun transient-prefix-object ()
  (or transient--prefix transient-current-prefix))
(setq magit-diff-refine-hunk (quote all))
#+end_src

*** C-x g ‚Üí magit-status

#+begin_src emacs-lisp
(global-set-key (kbd "C-x g") #'magit-status)
#+end_src

** Language server

#+begin_src emacs-lisp
(require 'eglot)
(add-to-list
 'eglot-server-programs
 `(elixir-ts-mode ,(file-name-concat user--package "elixir-ls/language_server.sh")))
#+end_src

* local extension

#+begin_src emacs-lisp
(defun extend-init-locally ()
  (let* ((env-var "EMACS_INIT_EXTENSION")
         (local-conf (getenv env-var)))
    (if (and local-conf (file-exists-p local-conf))
        (load-file local-conf)
      (message "INFO: no local extension to default init.el has been found.
Set %s to a local elisp file to run it after init.el"
               env-var))))
(extend-init-locally)
#+end_src

* Local Variables  :noexport:
Local Variables:
eval: (add-hook 'before-save-hook #'whitespace-cleanup)
eval: (add-hook 'after-save-hook #'org-babel-tangle)
eval: (indent-tabs-mode 0)
fill-column: 85
End:
